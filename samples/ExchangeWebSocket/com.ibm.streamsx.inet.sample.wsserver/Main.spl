namespace com.ibm.streamsx.inet.sample.wsserver ;

use com.ibm.streamsx.inet.rest::WebContext ;
use com.ibm.streamsx.inet.wsserver::WebSocketInject ;
use com.ibm.streamsx.inet.wsserver::WebSocketSend ;

/**
 * Example of using the Receive() and Send() WebSocket operators. 
 * 1. Start the application.
 * 2. Start up firefox on local system.
 * 3. go to "http://localhost:8080"
 * 4. select 'Start Transmitting'
 * 5. select 'Start Receiving'
 * 6. Notes the 'RECEIVED:" and 'TRANSMIT:' fields changing. 
 * 7. Move cursor into Gray box, move up/down to send data.
 */

public composite Main {
	param
		expression<int32> $webContextPort : (int32)getSubmissionTimeValue("webContextPort", "8080");
		expression<int32> $wsSourcePort :   (int32)getSubmissionTimeValue("wsSourcePort", "8081");
		expression<int32> $wsSinkPort :     (int32)getSubmissionTimeValue("wsSinkPort", "8082");
		
	graph
		()as WebContext_1 = WebContext(){
			param
				port : $webContextPort;
				context : "html";
				contextResourceBase : "opt/html";
		}

		stream<rstring data> ReceivedWsTuples = WebSocketInject() {
			param
				port : $wsSourcePort;
				ackCount : 10;
		}

		stream<rstring data> AugmentedWs as O = Functor(ReceivedWsTuples as I){
			logic
				state: {
					mutable list<rstring> colors = ["RED", "GREEN", "BLUE", "ORANGE", "PURPLE", "CORAL", "DEEPPINK", "LIME", "TAN", "SLATEBLUE"];
					mutable rstring  bstring;
					mutable rstring color;
				}
				onTuple I: {
					color = colors[(int32)(random() * 10.0)];
					// this will not work on a strictly HTML5 browser, but it's festive.
					bstring =  data + " Augment : <FONT COLOR=" + color + " >" + color + "</FONT>";
					printStringLn(bstring);
				}
				output O : data = bstring;
		}

		()as WsSender = WebSocketSend(AugmentedWs){
			param
				port : $wsSinkPort;
		}

}
